//import org.gradle.plugins.ide.eclipse.model.AccessRule

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = 11
compileJava.options.encoding = 'UTF-8'
version = versionFromProperties

apply from: file("${rootDir}/dependencies.gradle")

//eclipse {
//  classpath {
//    file {
//      whenMerged {
//        def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
//        jre.accessRules.add(new AccessRule('0', 'jdk/javadoc/**'))
//      }
//    }
//  }
//}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'res'
        }
    }
}


jar {

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveFileName = "bypsgen.jar"

    manifest {
        attributes 'Implementation-Title': 'BYPS generator',
                   'Implementation-Version': version,
                   'Main-Class': 'byps.gen.Main'
    }
 
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenCentral()
}

dependencies {
	implementation project(":byps-api")
	implementation project(":byps-override")
	implementation project(":byps")
	
	// https://mvnrepository.com/artifact/org.glassfish.jaxb/jaxb-runtime
	implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxbRuntimeVersion
	
 	implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
  	implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion
  
	implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion

	// https://mvnrepository.com/artifact/io.swagger.core.v3/swagger-models
	// BYPS-76: Version 2.2.18
	implementation group: 'io.swagger.core.v3', name: 'swagger-models', version: swaggerVersion

	implementation group: 'com.google.code.gson', name: 'gson', version:  gsonVersion

	implementation group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: jakartaWsRsVersion
}

publishing {

  publications {
    maven(MavenPublication) {
      groupId 'byps'
      artifact jar
      pom {
        name = 'BYPS'
        description = 'BYPS code generator'
        licenses {
          license {
            name = 'MIT'
            url = 'https://opensource.org/license/mit/'
          }
        }
      }
    }
  }

  repositories {
       // put the credentials into ~/.gradle/gradle.properties
      def mavenUrl = version.endsWith("SNAPSHOT") ? mavenUploadUrlSnapshots : mavenUploadUrl
  
    maven {
      allowInsecureProtocol = true
      url mavenUrl
      credentials { 
        username mavenUser 
        password mavenPassword 
      }
    }
  }
}

